1000 REM Combat subroutine
1005 REM Melee Combat - select combatants
1006 RANDOMIZE TIMER
1010 CLS: PRINT ,"Select combatants to roll for initiative"
1020 FOR Z=1 TO 25:IF NAM$(Z)="" THEN GOTO 1050
1030 PRINT Z; NAM$(Z)
1040 IF OOC(Z)=2 THEN LOCATE Z+1,45:PRINT "Unconscious/dead":GOTO 1049
1044 IF OOC(Z)=1 THEN LOCATE Z+1,45:PRINT "Out of combat":GOTO 1049
1048 IF STUN(Z)>0 THEN LOCATE Z+1, 45:PRINT "Stunned"
1049 NEXT Z
1050 PRINT "If only one person will fight this round, input their number now"
1052 PRINT "If a trap/similar is activated, also input ";Z;" now"
1055 INPUT I:IF I<>0 THEN A=I:GOTO 1700
1060 PRINT "Enter the numbers of all combatants to roll for initiative"
1061 FOR J = 1 TO 2
1065 Z=0
1070 INPUT I: IF I < 1 THEN GOTO 1119
1071 IF NAM$(I)="" THEN GOTO 1070
1075 IF NAM$(I)="" THEN PRINT "Invalid entry":GOTO 1070
1080 IF STUN(I)<=0 THEN GOTO 1090
1085 INPUT "That character is stunned. May they fight?";J$
1086 IF J$<>"y" AND J$<>"Y" THEN GOTO 1070
1090 IF OOC(I)=0 THEN GOTO 1100
1095 INPUT "That character is out of combat. May they fight?";J$
1096 IF J$<>"y" AND J$<>"Y" THEN GOTO 1070
1100 IF J = 1 THEN INIT(I)=INT((RND*10)+(MM(I)/5)+.5): GOTO 1104
1101 INPUT "Enter initiative roll";K: IF K < 1 OR K > 10 THEN GOTO 1101
1104 INIT(I) = K + MM(I)/5
1115 GOTO 1070
1119 IF J = 1 THEN PRINT "Enter the numbers of all candidates who have rolled their own initiative"
1120 NEXT J
1125 PRINT "Enter any character that has a penalty (or bonus) to their basic roll"
1130 INPUT I: IF I = 0 THEN GOTO 1140
1135 INPUT "Enter penalty";J: INIT(I) = INIT(I) - J: GOTO 1130
1140 IF INIT (I)<1 THEN INIT (I)=1
1150 IF INIT (I) > 15 THEN INIT (I) = 1
1161 REM haste routine: rem set haste to 1 for all
1165 FOR I=1 TO 30:HASTE(I)=1:NEXT I
1170 PRINT "Enter the number of any combatants hasted"
1180 INPUT I:IF I=0 THEN GOTO 1200
1190 INPUT "How many attacks may they make this round";J:HASTE(I)=J
1195 GOTO 1180
1200 INITIATIVE=15
1205 CLS
1210 PRINT "Initiative count-down for melee attacks"
1220 FOR Z=1 TO 25
1225 IF NAM$(Z)="" THEN GOTO 1300
1230 PRINT Z,NAM$(Z)
1240 NEXT Z
1300 PRINT "Initiative segment - ";INITIATIVE
1310 X=1
1315 FOR Z = 1 TO 25
1316 IF INIT(Z) >= INITIATIVE THEN GOTO 1320
1317 NEXT Z
1318 GOTO 1390
1320 FOR Z=1 TO 25
1330 IF INIT(Z) >= INITIATIVE AND STUN(Z) <= 0 AND OOC(Z) <= 0 THEN PRINT Z;" ";NAM$(Z);" may fight this round": COMB(X) = Z: X = X + 1: GOTO 1350
1340 IF INIT(Z)>=INITIATIVE AND (STUN(Z)>0 OR OOC(Z)>0) THEN PRINT Z;" ";NAM$(Z);" misses their combat turn"
1350 NEXT Z
1360 PRINT "Input numbers of other combatants (may override missed turns)"
1370 INPUT I:IF I=0 THEN GOTO 1390
1380 COMB(X)=I:X=X+1:GOTO 1370
1390 FOR I=1 TO 25
1392 IF HASTE(I)>1 AND INIT(I)>=INITIATIVE AND INITIATIVE>5 THEN INIT(I)=INIT(I)-5:HASTE(I)=HASTE(I)-1:GOTO 1396
1393 IF HASTE(I)>1 AND INIT(I)>= INITIATIVE THEN INIT(I)=1:HASTE(I)=HASTE(I)-1:GOTO 1396
1394 IF INIT(I)>=INITIATIVE THEN INIT(I)=0
1396 NEXT I
1398 COMBATANTS=X-1
1400 IF COMBATANTS=0 THEN INITIATIVE=INITIATIVE-1
1410 IF COMBATANTS=0 AND INITIATIVE>0 THEN GOTO 1205
1420 IF COMBATANTS=0 THEN GOTO 4000
1430 GOTO 2000
1500 REM Missile combatants
1505 RANDOMIZE TIMER
1510 CLS
1520 FOR Z=1 TO 25
1530 IF NAM$(Z)="" THEN GOTO 1550
1540 PRINT Z;NAM$(Z)
1545 IF OOC(Z)=2 THEN LOCATE Z+1,45:PRINT "Unconscious/dead":GOTO 1550
1546 IF OOC(Z)=1 THEN LOCATE Z+1,45:PRINT "Out of combat":GOTO 1550
1547 IF STUN(Z)>0 THEN LOCATE Z+1,45:PRINT "Stunned":GOTO 1550
1550 NEXT Z
1555 PRINT "Select numbers of combatants to fire this round"
1556 X=1
1560 INPUT I:IF I=0 THEN GOTO 1600
1570 IF NAM$(I)="" THEN GOTO 1560
1575 COMB(X)=I
1580 X=X+1:GOTO 1560
1600 COMBATANTS=X:INITIATIVE=1
1650 GOTO 2000
1700 REM Location for single melee combatant
1701 REM Resolution of traps
1710 INITIATIVE=1
1720 COMBATANTS=1
1730 COMB(1)=A
1740 IF NAM$(A)<>"" THEN GOTO 1800
1750 INPUT "What is the OB of the attack";I:OB1(A)=I
1760 INPUT "What is the OB type of the attack (0 to use table)";I: IF I <> 0 THEN OBT1(A)=I: GOTO 1770
1761 SOURCE = 3: CHAIN "characal", 1000, ALL
1765 OBT1(A) = TYPE
1770 INPUT "How many attacks are made?";I
1780 FOR Z=1 TO I
1785 COMB(Z)=A
1790 NEXT Z
1795 COMBATANTS=I
1800 GOTO 2000
2000 CLS:PRINT "The following are able to fight"
2005 Z=1
2010 X=COMB(Z)
2020 PRINT X,NAM$(X)
2030 Z=Z+1:IF COMB(Z)<>0 THEN GOTO 2010
2050 COMBATANT=1
2100 CLS:A=COMB(COMBATANT)
2110 PRINT NAM$(A);"'s attack"
2120 FOR Z=1 TO 25
2125 IF NAM$(Z)="" THEN GOTO 2145
2130 PRINT Z,NAM$(Z)
2135 IF OOC(Z)=2 THEN LOCATE Z+1,45:PRINT "Unconscious/dead":GOTO 2140
2136 IF OOC(Z)=1 THEN LOCATE Z+1,45:PRINT "Out of combat"
2137 IF STUN(Z)>0 THEN LOCATE Z+1,45:PRINT "Stunned"
2140 NEXT Z
2145 TARGET=1
2150 LOCATE Z+2,1:PRINT "Select target for attack - select 99 to skip or 0 to end input"
2151 INPUT I
2152 IF I=99 THEN GOTO 3385
2153 IF I>Z THEN GOTO 2151
2154 IF I=0 AND TARGET>1 THEN GOTO 2200
2155 IF I=0 THEN GOTO 2151
2156 IF A=I THEN PRINT "You can't pick yourself":GOTO 2151
2160 TAR(TARGET)=I
2170 TARGET=TARGET+1
2180 IF TARGET=11 THEN PRINT "No further targets":GOTO 2200
2190 GOTO 2151
2200 CLS:PRINT ,,"Details of combatants"
2210 PRINT "Attacker"
2220 PRINT "Name: ";NAM$(A);" OB(1): ";OB1(A);" OB(2): ";OB2(A);" Hits: ";HITS(A)
2225 PRINT
2230 PRINT "Defender(s)"
2231 TARGET=TARGET-1
2235 FOR X=1 TO TARGET
2236 D=TAR(X)
2240 PRINT "Name: ";NAM$(D);" DB:";DB(D);" Sh:";SH(D);" AT:";AT(D);" Hits:";HITS(D);" Hits left:";HITS(D)-DAM(D)
2241 NEXT X
2250 PRINT "Enter OB to use (1, 2, 3 {other}, 4 {multiple strikes})"
2255 NOATTACK=1:MULT=1
2260 INPUT I:IF I<>INT (I) OR I<1 OR I>4 THEN GOTO 2260
2270 ON I GOTO 2280,2290,2300,2310
2280 OB=OB1(A):OBMAX=OB1(A):OBT=OBT1(A)
2285 INPUT "Should I use default values? (Default Y)";I$
2286 IF I$<>"n" AND I$<>"N" THEN OB=OB1(A): OBT=OBT1(A): OBMAX = OB: OB = OB-DB(D): NOATTACK = 1: MULTI = 1: DEFENDER = 1: ATTACKS = 1: GOTO 2680
2287 GOTO 2350
2290 OB=OB2(A):OBMAX=OB:OBT=OBT2(A)
2295 INPUT "Should I use default values? (Default Y)";I$
2296 IF I$<>"n" AND I$<>"N" THEN OB=OB2(A): OBT=OBT2(A): OBMAX = OB: OB = OB-DB(D): NOATTACK = 1: MULTI = 1: DEFENDER = 1: ATTACKS = 1: GOTO 2680
2297 GOTO 2350
2300 INPUT "Enter OB value";OB:OBTOT=OB
2305 INPUT "Enter OB type (0 to use tables)";OBT:IF OBT <> 0 THEN GOTO 2350
2306 SOURCE = 4: CHAIN "characal", 1000, ALL
2308 OBT = TYPE
2310 INPUT "Enter number of attacks";NOATTACK
2350 INPUT "Is a maneuver required (default N)";I$:IF I$<>"y" AND I$<>"Y" GOTO 2370
2360 GOSUB 8000
2370 IF NOATTACK=1 THEN OBMAX=OB:GOTO 2450
2380 FOR X=1 TO NOATTACK
2390 INPUT "What is OB";I:ON I GOSUB 2410,2415
2400 NEXT X:GOTO 2450
2410 OBMAX=OBMAX+OB1(A):OBT=OBT1(A):RETURN
2415 OBMAX=OBMAX+OB2(A):OBT=OBT2(A):RETURN
2450 OBMAX=OBMAX*MULT
2460 IF MULT=99 THEN GOTO 5000:REM Combat maneuver fumble
2500 DEFENDER=1:ATTACKS=1
2510 D=TAR(DEFENDER)
2520 PRINT "Attacking ";NAM$(D)
2530 PRINT "OB remaining is ";OBMAX
2540 IF NOATTACK=1 THEN GOTO 2600
2550 INPUT "What is OB";I:ON I GOTO 2560,2570,2580
2560 OB=OB1(A):OBT=OBT1(A)
2565 GOTO 2600
2570 OB=OB2(A):OBT=OBT2(A)
2575 GOTO 2600
2580 INPUT "What is OB";OB
2585 INPUT "What is OB type";OBT
2600 INPUT "How much OB do you wish to use (default maximum)";I
2610 IF I=0 THEN OBMAX=OBMAX-(OB*MULT):OB=OB*MULT:GOTO 2630
2615 IF I>OBMAX THEN I=OBMAX
2620 OBMAX=OBMAX-I:OB=I
2630 INPUT "Input any penalties to attacker (including parry)";I:OB=OB-I
2631 OB=OB-DB(D)
2635 INPUT "Is defender's shield available (default Y)";I$:IF I$<>"n" AND I$<>"N" THEN GOTO 2640
2636 OB=OB+SH(D)
2640 PRINT "Enter any further modifiers"
2641 PRINT "Flank attack                - 1"
2642 PRINT "Rear attack                 - 2"
2643 PRINT "Defender surprised          - 3"
2644 PRINT "Defender down               - 4"
2645 PRINT "Attacker shifting equipment - 5"
2646 PRINT "No further input            - 0"
2647 INPUT I: IF I = 0 THEN GOTO 2680
2650 INPUT I:ON I GOTO 2655,2660,2665,2670,2675
2655 OB=OB+15:GOTO 2650
2660 OB=OB+20:GOTO 2650
2665 OB=OB+20:GOTO 2650
2670 OB=OB+20:GOTO 2650
2675 OB=OB-30:GOTO 2650
2680 IF STUN(D)>0 THEN OB=OB+20
2690 IF DAM(A)>HITS(A)/2 THEN OB=OB-20
2695 OB=OB-SUB(A)
2700 T=OBT-10
2705 IF T>37 THEN GOTO 2715
2710 ON T GOSUB 9010,9020,9025,9030,9035,9040,9050,9060,9065,9070,9080,9085,9090,9095,9300,9305,9315,9325,9330,9335,9340,9345,9350,9355,9360,9365,9370,9375,9380,9385,9390,9395,9400,9405,9410,9415,9420
2711 GOTO 2750
2715 T=T-37
2720 ON T GOSUB 9425,9430,9435,9440,9450,9455,,9460,9465,9470,9475,9480,9485,9490,9495,9500,9505,9510,9515,9520,9525,9530,9535,9545,9550,9555,9560,9565,9570,9575,9580,9585,9590,9591,9592,9593,9594,9595,9596,9597,9598,9599,9600,9601,9602,9603
2750 READ FUMBRANG:READ WEAPSTRE:READ BREASTRE:READ ATTKTABL:READ PRIMCRIT:READ MPRICRIT:READ SECNCRIT:READ MSECCRIT
2800 INPUT "Enter roll (enter 0 for random roll)";ROLL:IF ROLL=0 THEN GOSUB 9200
2805 PRINT "Roll was ";ROLL
2810 IF ROLL<FUMBRANG THEN GOTO 5100
2815 OB=OB+ROLL
2816 IF OB>250 THEN OB=250
2817 PRINT "Result was "; OB
2820 ON ATTKTABL GOTO 2830,2840,2850,2860,2870,2880
2830 CHAIN "1hedged",1000,ALL
2840 CHAIN "1hconcus",1000,ALL
2850 CHAIN "2handed",1000,ALL
2860 CHAIN "missile",1000,ALL
2870 CHAIN "toothclw",1000,ALL
2880 CHAIN "grappunb",1000,ALL
2900 RANDOMIZE TIMER
2910 IF CRIT=0 THEN GOTO 3000
2915 CRITL=CRIT:CRITF=CRITL
2920 PRINT "Is the critical pattern standard for this weapon?"
2925 PRINT "Note: do not include ADDITIONAL slaying attack (default Y)"
2926 INPUT I$
2927 CRIT2=CRIT-2
2928 NOCRITS=2
2930 IF I$="n" OR I$="N" THEN GOSUB 3700
2940 GOTO 3100
3000 REM No critical
3010 CLS:PRINT ,,"Combat result"
3015 PRINT "Final resolution was "; OB
3020 PRINT NAM$(A);" delivered ";HITS;" hits to ";NAM$(D)
3030 DAM(D)=DAM(D)+HITS
3040 IF DAM(D)>HITS(D) THEN PRINT NAM$(D);" is now unconscious from hits.":OOC(D)=2:KLL(D,A)=1:KILLED=1:KILLED(D)=1
3050 IF D<=PLAYNO THEN EP(D,1)=EP(D,1)+HITS:EP(D,8)=EP(D,8)+HITS
3055 INPUT I
3060 GOTO 3377
3100 REM Criticals
3101 NOCRIT = 1
3105 IF CRIT2>MSECCRIT THEN CRIT2=MSECCRIT
3106 IF SIZE(D)>1 THEN GOTO 3500
3110 CRITM=MPRICRIT:CRITT=PRIMCRIT
3120 IF CRIT>CRITM THEN CRIT=CRITM
3121 IF CRIT=-2 THEN PRINT "Critical level is T":GOTO 3130
3122 RESTORE 3980
3123 FOR I=1 TO CRIT
3124 READ I$:NEXT I
3125 PRINT "Critical level is ";I$
3130 INPUT "Enter critical roll (enter 0 for random roll)";ROLL
3140 IF ROLL=0 THEN ROLL=INT((RND*100)+.5)
3145 IF ROLL>100 THEN ROLL=100
3146 PRINT "Critical roll was "; ROLL
3150 INPUT "Enter any ambush variance";I:ROLL=ROLL+I:CPL=3
3160 ON CRITT GOTO 3161,3162,3163,3164,3165,3166,3167,3168,3169,3170,3171,3172,3173,3174,3175,3176,3177,,,,3178
3161 CHAIN "cruscr",20000,ALL
3162 CHAIN "slascr",20000,ALL
3163 CHAIN "punccr",20000,ALL
3164 CHAIN "unbacr",20000,ALL
3165 CHAIN "grapcr",20000,ALL
3166 CHAIN "heatcr",20000,ALL
3167 CHAIN "coldcr",20000,ALL
3168 CHAIN "eleccr",20000,ALL
3169 CHAIN "impacr",20000,ALL
3170 CHAIN "acidcr",20000,ALL
3171 CHAIN "disrcr",20000,ALL
3172 CHAIN "plascr",20000,ALL
3173 CHAIN "strecr",20000,ALL
3174 CHAIN "holycrit",20000,ALL
3175 CHAIN "holycrit",30000,ALL
3176 CHAIN "lockcr",20000,ALL
3177 CHAIN "nervcr",20000,ALL
3178 SL=1:CHAIN "slaycr",20000,ALL
3200 RANDOMIZE TIMER:CRITICAL(NOCRIT,1)=CHIT
3201 CRITICAL(NOCRIT,2)=CHRD
3202 CRITICAL(NOCRIT,3)=CPARR
3203 CRITICAL(NOCRIT,4)=CNOPA
3204 CRITICAL(NOCRIT,5)=CSTUN
3205 CRITICAL(NOCRIT,6)=CSUB
3206 CRITICAL(NOCRIT,7)=CPLUS
3207 CRITICAL(NOCRIT,8)=COOC
3208 CRIT$(NOCRIT)=C$
3210 IF CRIT2>0 THEN CRIT=CRIT2:CRITT=SECNSCRIT:NOCRIT=2:CRIT2=0:GOTO 3121
3220 CLS:PRINT,,"Combat result"
3230 PRINT NAM$(D);" took the following critical strike."
3240 FOR I=1 TO NOCRIT
3245 PRINT CRIT$(I):NEXT I
3250 THITS=HITS+CRITICAL(1,1)+CRITICAL(2,1)
3251 THRD=CRITICAL(1,2)+CRITICAL(2,2)
3252 TPARR=CRITICAL(1,3)+CRITICAL(2,3)
3253 TNOPA=CRITICAL(1,4)+CRITICAL(2,4)
3254 TSTUN=CRITICAL(1,5)+CRITICAL(2,5)
3255 TSUB=CRITICAL(1,6)+CRITICAL(2,6)
3256 TPLUS=CRITICAL(1,7)+CRITICAL(2,7)
3257 IF CRITICAL (1,8)>=CRITICAL(2,8) THEN TOOC=CRITICAL(1,8)
3258 IF CRITICAL (2,8)>CRITICAL(1,8) THEN TOOC=CRITICAL(1,8)
3260 PRINT "Associated with this were:"
3261 IF THITS>0 THEN PRINT THITS;" hits"
3262 IF THRD>0 THEN PRINT THRD;" hits per round"
3263 IF TPARR>0 THEN PRINT NAM$(D);" must parry for the next ";TPARR;" rounds"
3264 IF TNOPA>0 THEN PRINT NAM$(D);" may not parry for the next ";TPARR;" rounds"
3265 IF TSTUN>0 THEN PRINT TSTUN;" rounds stunned"
3266 IF TSUB>0 THEN PRINT TSUB;" subtraction from bonuses"
3267 IF TPLUS>0 THEN PRINT NAM$(A);" has a bonus of ";TPLUS;" to their next attack"
3268 IF TOOC=1 AND TOOC>OOC(D) THEN PRINT NAM$(D);" is out of combat"
3269 IF TOOC=2 AND TOOC>OOC(D) THEN PRINT NAM$(D);" is unconscious/dead"
3270 PRINT:PRINT "Do you accept this (default Y)"
3275 INPUT I$:IF I$<>"n" AND I$<>"N" THEN GOTO 3300
3280 PRINT "Select section to ignore"
3281 PRINT "Hits (only from critical)   - 1"
3282 PRINT "Bleeding                    - 2"
3283 PRINT "Rounds parry                - 3"
3284 PRINT "Rounds no parry             - 4"
3285 PRINT "Stun                        - 5"
3286 PRINT "Subtraction from bonuses    - 6"
3287 PRINT "Out of combat               - 7"
3288 PRINT "Entire critical 1           - 8"
3289 PRINT "Entire critical 2           - 9"
3290 INPUT I:IF I<>INT(I) OR I<1 OR I>9 THEN GOTO 3300
3291 IF I<7 THEN CRITICAL (1,I)=0:CRITICAL(2,I)=0:GOTO 3290
3292 IF I=7 THEN CRITICAL (1,8)=0:CRITICAL(2,8)=0:GOTO 3290
3293 I=I-7
3294 FOR J=1 TO 8
3295 CRITICAL (I,J)=0
3296 NEXT J
3297 GOTO 3290
3300 DAM(D)=DAM(D)+HITS+CRITICAL(1,1)+CRITICAL(2,1)
3301 HRD(D)=HRD(D)+CRITICAL(1,2)+CRITICAL(2,2)
3302 STUN(D)=STUN(D)+CRITICAL(1,5)+CRITICAL(2,5)
3303 SUB(D)=SUB(D)+CRITICAL(1,6)+CRITICAL(2,6)
3304 IF A>PLAYNO THEN GOTO 3310
3305 FOR J=1 TO 2
3306 IF CRITICAL(J,8)=2 THEN KILLED=1:KLL(D,A)=1:KILLED(D)=1
3307 NEXT J
3308 IF CRITL<0 THEN CRITL=0
3310 IF D<=PLAYNO THEN EP(D,1)=EP(D,1)+HITS+CRITCAL(1,1)+CRITICAL(2,1):EP(D,2)=EP(D,2)+(100*CRITL):EP(D,8)=EP(D,8)+HITS+CRITCAL(1,1)+CRITCAL(2,1)+(100*CRITL)
3315 IF A<=PLAYNO AND CRITL>0 THEN GOSUB 3800
3320 IF A>PLAYNO THEN GOTO 3325
3321 IF DAM(D)>HITS(D) AND OOC(D)<2 THEN PRINT NAM$(D);" is now unconscious from hits.":OOC(D)=2:KILLED=1:KLL(D,A)=1:KILLED(D)=1
3325 IF DAM(D)>HITS(D) AND OOC(D)<2 THEN PRINT NAM$(D);" is now unconscious from hits.":OOC(D)=2
3345 IF CRITICAL (1,8)>OOC(D) THEN OOC(D)=CRITICAL(1,8)
3346 IF CRITICAL (2,8)>OOC(D) THEN OOC(D)=CRITICAL(1,8)
3350 IF NOCRITS>2 THEN GOTO 3400
3360 INPUT "Are there to be any further criticals (default N)";I$
3370 IF I$="y" OR I$="Y" THEN GOTO 3400
3375 IF CRITICAL(1,8)>OOC(D) THEN OOC(D)=CRITICAL(1,8)
3376 IF CRITICAL(2,8)>OOC(D) THEN OOC(D)=CRITICAL(2,8)
3377 IF ATTACKS<NOATTACK AND (TARGET-DEFENDER)<=(NOATTACK-ATTACKS) THEN INPUT "Attack the same target again";I$:REPEAT=1
3378 IF REPEAT = 1 AND (I$="y" OR I$="Y") THEN ATTACKS=ATTACKS+1:REPEAT=0:GOTO 2510
3379 ATTACKS=ATTACKS+1
3380 DEFENDER=DEFENDER+1:IF DEFENDER<=TARGET THEN GOTO 2510
3381 GOSUB 6200
3385 COMBATANT=COMBATANT+1
3387 GOSUB 6000
3390 IF COMB(COMBATANT)>0 THEN GOTO 2100
3391 IF KILLED=1 THEN GOSUB 3600
3395 INITIATIVE=INITIATIVE-1
3396 IF INITIATIVE>0 THEN GOTO 1205
3399 GOTO 4000
3400 NOCRITS=NOCRITS-1:CRITL=0
3405 IF CRITL=-2 THEN PRINT "Initial critical level was T":GOTO 3430
3410 RESTORE 3980
3415 FOR I=1 TO CRITF
3420 READ I$:NEXT I
3425 PRINT "Initial critical level was ";I$
3430 PRINT "Select type of critical"
3431 PRINT "Crush                       - 1"
3432 PRINT "Slash                       - 2"
3433 PRINT "Puncture                    - 3"
3434 PRINT "Unbalance                   - 4"
3435 PRINT "Grapple                     - 5"
3436 PRINT "Heat                        - 6"
3437 PRINT "Cold                        - 7"
3438 PRINT "Electricity                 - 8"
3439 PRINT "Impact                      - 9"
3440 PRINT "Acid                        - 10"
3441 PRINT "Disruption                  - 11"
3442 PRINT "Plasma                      - 12"
3443 PRINT "Stress                      - 13"
3444 PRINT "Holy physical               - 14"
3445 PRINT "Holy spell                  - 15"
3446 PRINT "Locking holds               - 16"
3447 PRINT "Nerve strikes               - 17"
3448 PRINT "Slaying                     - 18"
3450 INPUT I:IF I<>INT (I) OR I<1 OR I>18 THEN GOTO 3450
3460 CRITT=I:IF CRITT=18 THEN CRITT=21
3465 INPUT "Enter level of critical";I:CRIT=I
3470 GOTO 3130
3500 IF CRITL>1 THEN GOTO 3507
3501 FOR I=1 TO 8
3502 CRITICAL(NOCRIT,I)=0
3505 NEXT I
3506 GOTO 3210
3507 INPUT "Enter roll (enter 0 for random roll)";ROLL
3508 IF ROLL=0 THEN GOSUB 9100
3509 INPUT "Enter ambush variance";I:ROLL=ROLL+I
3510 INPUT "Is attack slaying (default N)";I$:IF I$<>"y" AND I$<>"Y" THEN GOTO 3521
3520 SL=SIZE(D)-1:IF SL<1 THEN SL=1
3521 PTN=3
3530 PRINT "Enter type of weapon"
3531 PRINT "Normal weapon               - 1"
3532 PRINT "Magic weapon                - 2"
3533 PRINT "Mithril weapon              - 3"
3534 PRINT "Holy weapon                 - 4"
3535 INPUT I:IF I<>INT(I) OR I<1 OR I>4 THEN GOTO 3535
3540 ROLL=ROLL-30+I*10
3545 PRINT "Critical roll was "; ROLL
3550 CHAIN"slaycr",20000,ALL
3599 GOTO 3200
3600 FOR D = 1 TO 25
3605 KILLERS = 0
3610 FOR A = 1 TO PLAYNO
3620 IF KLL(D, A) = 1 THEN KILLERS = KILLERS + 1
3625 NEXT A
3626 IF KILLERS = 0 THEN GOTO 3690
3630 CRITEP = 0
3635 FOR A = 1 TO PLAYNO
3640 CRITEP = CRITEP + CEXP(D, A): CEXP(D, A) = 0
3645 NEXT A
3650 FOR A = 1 TO PLAYNO
3660 IF KLL(D, A) = 0 THEN GOTO 3685
3665 KLL(D, A) = 0
3670 KILLPTS = 200 + 50 * LVL(D) - 50 * LVL(A)
3675 KILLPTS = KILLPTS - CRITEP: IF KILLPTS < 50 THEN KILLPTS = 50
3676 KILLPTS = INT((KILLPTS / KILLERS) + .5)
3680 EP(A, 3) = EP(A, 3) + KILLPTS: EP(A, 8) = EP(A, 8) + KILLPTS
3685 NEXT A
3690 NEXT D
3699 RETURN
3700 CLS:PRINT "Enter type of primary critical"
3701 PRINT "Crush                       - 1"
3702 PRINT "Slash                       - 2"
3703 PRINT "Puncture                    - 3"
3704 PRINT "Unbalance                   - 4"
3705 PRINT "Grapple                     - 5"
3706 PRINT "Heat                        - 6"
3707 PRINT "Cold                        - 7"
3708 PRINT "Electricity                 - 8"
3709 PRINT "Impact                      - 9"
3710 PRINT "Acid                        - 10"
3711 PRINT "Disruption                  - 11"
3712 PRINT "Plasma                      - 12"
3713 PRINT "Stress                      - 13"
3714 PRINT "Holy physical               - 14"
3715 PRINT "Holy spell                  - 15"
3716 PRINT "Locking holds               - 16"
3717 PRINT "Nerve strikes               - 17"
3718 PRINT "Slaying                     - 18"
3720 INPUT I:IF I<>INT (I) OR I<1 OR I>18 THEN GOTO 3720
3721 IF I=18 THEN I=21
3730 PRIMCRIT=I:CRITT=I
3740 INPUT "Enter type of secondary critical (0 if none)";I
3745 SECNCRIT=I
3750 IF I>0 THEN INPUT "Enter levels below primary critical";I
3755 CRIT2=CRIT-I:MSECCRIT=CRIT2
3760 IF I>0 THEN CRIT2=CRIT-I
3770 RETURN
3800 REM Give critical XP
3810 EXPOINT=5*CRITL*LVL(D)
3811 IF OOC(D)=2 THEN EXPOINT=EXPOINT/10:GOTO 3820
3815 IF STUN(D)>0 THEN EXPOINT=EXPOINT/2
3820 EP(A,2)=EP(A,2)+EXPOINT:EP(A,8)=EP(A,8)+EXPOINT
3830 CEXP(D,A)=CEXP(D,A)+EXPOINT
3840 RETURN
3980 DATA "A", "B", "C", "D", "E", "F", "G", "H", "I", "J"
4000 REM Clear variables and leave
4010 FOR I=1 TO 2
4011 FOR J=1 TO 8
4015 CRITICAL(I,J)=0
4016 NEXT J
4017 CRIT$(I)=""
4020 NEXT I
4030 FOR I=1 TO 25
4031 INIT(I)=0
4032 HASTE(I)=0
4033 COMB(I)=0
4034 KILLED(I)=0
4035 NEXT I
4040 Z=0:I=0:A=0:X=0:D=0:C$="":I$="":J$=""
4045 INITIATIVE=0:COMBATANTS=0:TARGET=0:NOATTACK=0
4050 FOR I=1 TO 10:TAR(I)=0:NEXT I
4055 MULT=1:OB=0:OBTOT=0:OBT=0:OBMAX=0
4060 DEFENDER=0:FUMBRANG=0:WEAPSTRE=0:BREASTRE=0
4065 ATTKTABL=0:PRIMCRIT=0:MPRICRIT=0:SECNCRIT=0:MSECCRIT=0
4070 ROLL=0:CRITL=0:CRIT2=0:NOCRITS=0:CRITM=0
4075 NOCRIT=0:CPL=0:SL=0
4080 CHITS=0:CHRD=0:CPARR=0:CNOPA=0:CSTUN=0:CSUB=0:CPLUS=0:COOC=0
4085 CRITT=0
4090 THITS=0:THRD=0:TPARR=0:TNOPA=0:TSTUN=0:TSUB=0:TPLUS=0:TOOC=0
4095 KILLED=0:CRITF=0:PTN=0:CRITEP=0:KILLPTS=0:EXPOINT=0:DIFF=0
4100 FHIT=0:FHRD=0:FSTUN=0:FSUB=0:FOOC=0
4105 SKILL=0
4110 CHAIN "merp",500,ALL
5000 REM Weapon fumble
5010 INPUT "Enter fumble roll (enter 0 for random roll)";ROLL
5020 IF ROLL=0 THEN ROLL=INT((RND*100)+.5)
5025 RESTORE 5900
5030 READ I:READ J:IF DIFF>I THEN GOTO 5030
5040 ROLL=ROLL+J:IF ROLL>120 ROLL=120
5050 RESTORE 5901
5060 READ I:READ I$:READ FHIT:READ FHRD:READ FSTUN:READ FSUB:READ FOOC:IF ROLL>I THEN GOTO 5060
5070 CLS:PRINT "The maneuver was fumbled. ";NAM$(A);" took the following:"
5080 PRINT I$
5081 IF FHIT>0 THEN PRINT "Hits taken: ";FHIT:DAM(A)=DAM(A)+FHIT
5082 IF FHRD>1 THEN PRINT "Bleeding: ";FHRD:HRD(A)=HRD(A)+FHRD
5083 IF FSTUN>1 THEN PRINT "Rounds stunned: ";FSTUN:STUN(A)=STUN(A)+FSTUN
5084 IF FSUB>0 THEN PRINT "Penalty to activity: ";FSUB:SUB(A)=SUB(A)+FSUB
5085 IF FOOC>OOC(A) THEN OOC(A)=FOOC
5086 IF A<=PLAYNO THEN EP(A,1)=EP(A,1)+FHIT
5087 INPUT I
5090 GOTO 3385
5100 IF OBT>67 THEN GOTO 5200
5105 RESTORE 5920
5110 READ I:READ J:IF OBT>I THEN GOTO 5110
5115 INPUT "Enter fumble roll (enter 0 for random roll)";ROLL
5120 IF ROLL=0 THEN ROLL=INT((RND*100)+.5)
5130 RESTORE 5921
5135 FOR I=1 TO 12
5140 READ J:IF OBT=J THEN J=10:GOTO 5150
5145 NEXT I
5150 PRINT "Is ";NAM$(A);" mounted? (default N)"
5155 INPUT I$: IF I$="y" OR I$="Y" THEN J=20
5160 ROLL=ROLL+J:IF ROLL>120 THEN ROLL=120
5161 RESTORE 5930
5165 READ I:READ I$:READ FHIT:READ FHRD:READ FSTUN:READ FSUB: READ FOOC
5166 IF ROLL>I THEN GOTO 5165
5170 CLS:PRINT "The attack was fumbled. ";NAM$(A);" took the following: "
5180 GOTO 5080
5200 RESTORE 5950
5210 FOR I=1 TO 5
5215 READ J:READ K:IF OBT=J THEN GOTO 5225
5220 NEXT I
5221 K=-20
5225 INPUT "Enter fumble roll (enter 0 for random roll)";ROLL
5230 IF ROLL=0 THEN ROLL=INT((RND*100)+.5)
5240 ROLL=ROLL+K
5241 RESTORE 5951
5250 READ I:READ I$:READ FHIT:READ FHRD:READ FSTUN:READ FSUB:READ FOOC
5255 IF ROLL>I THEN GOTO 5250
5260 GOTO 5170
5900 DATA 1,-50,2,-35,3,-20,4,-10,5,0,6,5,7,10,8,15,9,20
5901 DATA 5,"You hesitate and fail to act.",,,,,
5902 DATA 20,"You have second thoughts, and decide to wait one round.",,,,,
5903 DATA 35,"You slip. 30% chance of falling. -20 from any maneuvers for 2 rounds",,,,,
5904 DATA 50,"You stumble. 45% chance of falling. -30 from any maneuvers for 2 rounds.",,,,,
5905 DATA 65,"You stub your toe. 60% chance of falling.",3,,,10,
5906 DATA 79,"You slip. 75% chance of falling.",,,2,,
5907 DATA 80,"You twist your ankle.",5,,,10,
5908 DATA 86,"You fall down. -20 to activity for 3 rounds.",3,,,,
5909 DATA 89,"You sprain your ankle and tear some tendons.",7,,1,20,
5910 DATA 90,"Fall breaks your leg",8,,3,30,
5911 DATA 96,"You break your wrist when you fall.",12,,2,20,
5912 DATA 99,"Your arm breaks when you fall on it.",14,,4,30,
5913 DATA 100,"In an attempt to break your fall you break both of your arms; they are useless.",30,,6,,1
5914 DATA 106,"When you fall your leg twists under you and breaks.",15,,3,50,
5915 DATA 109,"Your knee strikes a hard object and shatters when you fall.",10,,4,80,
5916 DATA 110,"You fall and the resulting concussion results in a year-long coma",,,,,2
5917 DATA 116,"You fall and land on your lower spine. You are paralyzed from the waist down",30,,,,
5918 DATA 119,"You fall and are paralyzed from the neck down.",20,,,,1
5919 DATA 120,"Your fall turns into a dive. You crush your skull and die.",,,,,2
5920 DATA 34,-10,47,-20,67,0
5921 DATA 34,28,31,32,33,61,65,63,66,64,57,62
5930 DATA 5,"Lose your grip. No further activity this round.",,,,,
5931 DATA 20,"You slip. If your weapon is 1-handed and non-magic, it breaks.",,,,,
5932 DATA 35,"Bad follow-through. You lost your opportunity.",2,,,,
5933 DATA 50,"Drop your weapon. It will take 1 round to draw a new one, or 2 rounds to recoverold one.",,,,,
5934 DATA 65,"You lose your 'wind' and realise you should try to relax. -40 to activity for 2 rounds.",,,,,
5935 DATA 79,"You stumble. The classless display leaves you stunned. With luck, you might     still  survive.",,,2,,
5936 DATA 80,"Incredibly inept move. Roll a 'B' crush critical on yourself. If opponent is    using a slashing weapon, your weapon is broken.",,,,,
5937 DATA 86,"Bite and swallow tongue in the excitment",,,2,,
5938 DATA 89,"Lose your grip on your weapon and reality.",,,3,,
5939 DATA 90,"Bad taste and poor execution. You attempt to maim yourself as your weapon breaksRoll a 'C' slash critical on yourself.",,,,,
5940 DATA 96,"Unbelievable mishandling of your weapon. Any friendly combatant in your vicinitytakes a 'B' crush critical.",,,,,
5941 DATA 99,"Stumble over an imaginary deceased turtle. You are very confused.",,,3,,
5942 DATA 100,"Worst move seen in ages. You pull your groin. Foe is stunned 2 rounds laughing.",,,,60,
5943 DATA 106,"You stagger and fall in an apparent attempt to commit suicide. If using a pole  arm, its shaft is shattered.",,,3,,
5944 DATA 109,"You break your weapon through ineptness",,,4,,
5945 DATA 110,"You stumble and drive the point of your weapon into the ground. If mounted: you pole-vault 30 feet and take a 'C' crush critical from the landing",,,5,,
5946 DATA 116,"Your mount rears suddenly. You must recover.",,,3,,
5947 DATA 119,"You do not coordinate your movement with your mount's. -90 to activity for the  next 3 rounds trying to stay mounted.",,,,,
5948 DATA 120,"You fall of your mount. Roll a 'D' crush critical on yourself.",,,,,
5950 DATA 76,0,77,20,78,20,70,10,72,-10
5951 DATA 5,"Lose your grip. No further activity this round.",,,,,
5952 DATA 20,"One's ten thumbs cannot handle loading. Lose this round.",,,,,
5953 DATA 35,"Fumble ammunition. Lose this round. -50 to activity next round.",,,,,
5954 DATA 50,"Break ammunition and lose your cool. You find yourself at -30 activity for threerounds of action.",,,,,
5955 DATA 65,"Drop ammunition. Stunned while trying to decide whether to retrieve it.",,,2,,
5956 DATA 79,"You really mishandle your weapon.",,,2,,
5957 DATA 80,"Poor judgement. If using a (non cross) bow, you let arrow fly, lose an ear, and take 2 hits per round.",5,,,,
5958 DATA 86,"Bowstring breaks. It will take 2 roudns to draw a new weapon or 6 rounds to     restring bow.",,,,,
5959 DATA 89,"Fumble ammunition when loading. You scatter all of your ammunition oer a 10 footradius area.",,,,,
5960 DATA 90,"Weapon shatters. Good luck, pal.",,,4,,
5961 DATA 96,"You let your arrow fly much too soon. You strike 20 feet short of target. You   are at -30 activity for 3 rounds.",,,,,
5962 DATA 99,"You seem to think that your bow is a baton. It slips and in trying to recover   it, you knock it 5 feet in front of you.",,,,,
5963 DATA 100,"Your ammunition slips as you fire. The missile goes through your hand. Hand     useless.",8,2,,,
5964 DATA 106,"Slip and fall down. Your shot goes astray.",,,5,,
5965 DATA 109,"Fletching on missile scratches eye as it is released.",5,,2,20,
5966 DATA 110,"Tip of weapon catches on closest object and breaks off. If applicable, the      object takes an 'A' puncture critical.",,,,,
5967 DATA 116,"Trigger slips while you are bringing up your weapon. Make an attack with no     modifications on the closest combatant.",,,,,
5968 DATA 119,"While daydreaming you put your hand in front of the bolt while firing. Lose a   finger.",4,2,,,
5969 DATA 120,"You slip and pin your foot to the ground with a bolt",10,2,3,30,
6000 REM Clear variables for next attack
6045 COMBATANTS=0:TARGET=0:NOATTACK=0
6050 FOR I=1 TO 10:TAR(I)=0:NEXT I
6055 MULT=1:OB=0:OBTOT=0:OBT=0:OBMAX=0
6105 SKILL=0
6110 RETURN
6200 REM End of attack
6210 FOR I=1 TO 2
6211 FOR J=1 TO 8
6215 CRITICAL (I,J)=0
6216 NEXT J
6217 CRIT$(I)=""
6220 NEXT I
6240 Z=0:I=0:A=0:X=0:D=0:C$="":I$="":J$=""
6260 DEFENDER=0:FUMBRANG=0:WEAPSTRE=0:BREASTRE=0
6265 ATTKTABL=0:PRIMCRIT=0:MPRICRIT=0:SECNCRIT=0:MSECCRIT=0
6270 ROLL=0:CRITL=0:CRIT2=0:NOCRITS=0:CRITM=0
6275 NOCRIT=0:CPL=0:SL=0
6280 CHITS=0:CHRD=0:CPARR=0:CNOPA=0:CSTUN=0:CSUB=0:CPLUS=0:COOC=0
6285 CRITT=0
6290 THITS=0:THRD=0:TPARR=0:TNOPA=0:TSTUN=0:TSUB=0:TPLUS=0:TOOC=0
6295 CRITF=0:PTN=0:CRITEP=0:KILLPTS=0:EXPOINT=0:DIFF=0
6300 FHIT=0:FHRD=0:FSTUN=0:FSUB=0:FOOC=0
6305 SKILL=0
6400 RETURN
8000 CLS:PRINT "Enter the difficulty of the maneuver"
8001 PRINT "Routine                     - 1"
8002 PRINT "Easy                        - 2"
8003 PRINT "Light                       - 3"
8004 PRINT "Medium                      - 4"
8005 PRINT "Hard                        - 5"
8006 PRINT "Very Hard                   - 6"
8007 PRINT "Extremely Hard              - 7"
8008 PRINT "Sheer Folly                 - 8"
8009 PRINT "Absurd                      - 9"
8010 INPUT I: IF I<>INT (I) OR I<1 OR I>9 THEN GOTO 8010
8020 DIFF=I
8030 INPUT "Enter skill bonus";SKILL
8040 IF STUN(A)>0 THEN SKILL=SKILL-50
8050 INPUT "Enter any further bonuses to attacker";I:SKILL=SKILL+I
8060 INPUT "Enter dice roll (enter 0 to allow random number generation)";ROLL
8070 IF ROLL=0 THEN GOSUB 9100
8080 ROLL=ROLL+SKILL
8100 ON DIFF GOTO 8111,8112,8113,8114,8115,8116,8117,8118,8119
8111 RESTORE 8151:GOTO 8120
8112 RESTORE 8152:GOTO 8120
8113 RESTORE 8153:GOTO 8120
8114 RESTORE 8154:GOTO 8120
8115 RESTORE 8155:GOTO 8120
8116 RESTORE 8156:GOTO 8120
8117 RESTORE 8157:GOTO 8120
8118 RESTORE 8158:GOTO 8120
8119 RESTORE 8159:GOTO 8120
8120 IF ROLL>300 THEN ROLL=300
8130 READ I:READ J:IF ROLL>I THEN GOTO 8130
8135 MULT=J/100:IF MULT>1 THEN MULT=1
8140 PRINT "OB is multiplied by ";MULT
8150 INPUT I:RETURN
8151 DATA -151,99,-101,10,-51,30,-26,50,0,70,20,80,40,90,300,100
8152 DATA -101,99,-51,10,-26,30,0,50,20,60,40,70,55,80,65,90,300,100
8153 DATA -51,99,-26,10,0,30,20,50,40,60,55,70,65,80,75,90,300,100
8154 DATA -26,99,0,5,20,10,40,20,55,30,65,40,75,50,85,60,95,70,105,80,115,90,300,100
8155 DATA 0,99,20,5,40,10,55,20,65,30,75,40,85,50,95,60,105,70,115,80,125,90,300,100
8156 DATA 20,99,40,5,55,10,65,20,75,30,85,40,95,50,105,60,115,70,125,80,135,90,300,100
8157 DATA 40,99,55,5,65,10,75,20,85,30,95,40,105,50,115,60,125,70,135,80,145,90,300,100
8158 DATA 65,99,75,5,85,10,95,20,105,25,115,30,125,40,135,50,145,60,155,70,165,80,185,90,300,100
8159 DATA 85,99,95,5,105,10,115,20,125,30,135,40,145,50,155,60,165,70,185,80,225,90,300,100
9000 REM Weapon types
9010 IF AT(D)>12 THEN OB=OB+5
9015 RESTORE 10011:RETURN
9020 RESTORE 10012:RETURN
9025 RESTORE 10013:RETURN
9030 RESTORE 10014:RETURN
9035 RESTORE 10015:RETURN
9040 IF AT(D)>12 THEN OB=OB-15
9045 RESTORE 10016:RETURN
9050 IF AT(D)>12 THEN OB=OB-10
9055 RESTORE 10017:RETURN
9060 RESTORE 10018:RETURN
9065 RESTORE 10019:RETURN
9070 IF AT(D)>12 THEN OB=OB+5
9075 RESTORE 10020:RETURN
9080 RESTORE 10021:RETURN
9085 RESTORE 10022:RETURN
9090 RESTORE 10023:RETURN
9095 RESTORE 10024:RETURN
9100 REM Open-ended (double) roll
9110 ROLL=0
9115 X=INT((RND*100)+.5)
9120 ROLL=ROLL+X
9125 IF X>95 THEN GOTO 9150
9130 IF X<6 THEN GOTO 9180
9140 RETURN
9150 X=INT((RND*100)+.5)
9155 ROLL=ROLL+X
9160 IF X>95 THEN GOTO 9150
9170 RETURN
9180 X=INT((RND*100)+.5)
9185 ROLL=ROLL-X
9190 IF X>95 THEN GOTO 9180
9195 RETURN
9200 REM Open-ended (high) roll
9210 ROLL=0
9215 X=INT((RND*100)+.5)
9220 ROLL=ROLL+X
9225 IF X>95 THEN GOTO 9215
9230 RETURN
9300 RESTORE 10025:RETURN
9305 IF AT(D)>12 THEN OB=OB-10
9310 OB=OB+5:RESTORE 10026:RETURN
9315 IF AT(D)>12 THEN OB=OB-20
9320 OB=OB+10:RESTORE 10027:RETURN
9325 RESTORE 10028:RETURN
9330 RESTORE 10029:RETURN
9335 RESTORE 10030:RETURN
9340 RESTORE 10031:OB=OB-10:RETURN
9345 OB=OB-5:RESTORE 10032:RETURN
9350 OB=OB-5:RESTORE 10033:RETURN
9355 OB=OB-10:RESTORE 10034:RETURN
9360 RESTORE 10035:RETURN
9365 RESTORE 10036:OB=OB+10:RETURN
9370 RESTORE 10037:RETURN
9375 RESTORE 10038:RETURN
9380 RESTORE 10039:RETURN
9385 RESTORE 10040:RETURN
9390 RESTORE 10041:OB=OB+5:RETURN
9395 OB=OB-10:RESTORE 10042:RETURN
9400 RESTORE 10043:RETURN
9405 RESTORE 10044:OB=OB-5:RETURN
9410 OB=OB-10:RESTORE 10045:RETURN
9415 OB=OB-5:RESTORE 10046:RETURN
9420 RESTORE 10047:RETURN
9425 RESTORE 10048:RETURN
9430 RESTORE 10049:RETURN
9435 RESTORE 10050:RETURN
9440 IF AT(D)>12 THEN OB=OB+10
9445 OB=OB-5:RESTORE 10051:RETURN
9450 RESTORE 10052:RETURN
9455 RESTORE 10053:OB=OB+10:RETURN
9460 RESTORE 10055:RETURN
9465 OB=OB-10:RESTORE 10056:RETURN
9470 OB=OB-10:RESTORE 10057:RETURN
9475 RESTORE 10058:RETURN
9480 RESTORE 10059:RETURN
9485 RESTORE 10060:RETURN
9490 RESTORE 10061:RETURN
9495 RESTORE 10062:OB=OB+5:RETURN
9500 RESTORE 10063:RETURN
9505 OB=OB+5:RESTORE 10064:RETURN
9510 OB=OB-5:RESTORE 10065:RETURN
9515 OB=OB+15:RESTORE 10066:RETURN
9520 OB=OB-10:RESTORE 10067:RETURN
9525 OB=OB-10:RESTORE 10068:RETURN
9530 OB=OB-10:RESTORE 10069:RETURN
9535 INPUT "How many rounds loading";I:IF I<1 THEN OB=OB-35
9536 INPUT "Enter range (feet)";I
9540 OB=OB-25*(INT(I/100)):RESTORE 10070:RETURN
9545 INPUT "Enter range (feet)";I:OB=OB-25*(INT(I/40)):RESTORE 10071:RETURN
9550 INPUT "How many rounds loading";I:IF I<1 THEN OB=OB-10
9551 INPUT "Enter range (feet)";I:OB=OB-(25*INT(I/60)):RESTORE 10072:RETURN
9555 RESTORE 10073:RETURN
9560 PRINT "Reminder: requires one round to load"
9561 INPUT "Enter range (feet)";I:OB=OB-(25*INT(I/50)):RESTORE 10074:RETURN
9565 INPUT "Enter range (feet)";I:OB=OB-(25*INT(I/50)):RESTORE 10075:RETURN
9570 INPUT "How many rounds loading";I:IF I<1 THEN OB=OB-25
9571 INPUT "Enter range (feet)";I:OB=OB-(25*INT(I/75)):RESTORE 10076:RETURN
9575 PRINT "Reminder: requires two rounds to load"
9576 INPUT "Enter range (feet)";I:OB=OB-(25*INT(I/100)):RESTORE 10077
9577 IF I<50 THEN OB=OB+30
9578 RETURN
9580 PRINT "Reminder: requires two rounds to load"
9581 INPUT "Enter range (feet)";I:OB=OB-(25*INT(I/100)):RESTORE 10078
9582 IF I<50 THEN OB=OB+20
9583 RETURN
9585 RESTORE 10079:RETURN
9590 RESTORE 10080:RETURN
9591 RESTORE 10081:RETURN
9592 RESTORE 10082:RETURN
9593 RESTORE 10083:RETURN
9594 RESTORE 10084:RETURN
9595 RESTORE 10085:RETURN
9596 INPUT "Enter distance fallen";OB:RESTORE 10086:RETURN
9597 RESTORE 10087:RETURN
9598 RESTORE 10088:RETURN
9599 RESTORE 10089:RETURN
9600 RESTORE 10090:RETURN
9601 RESTORE 10091:RETURN
9602 RESTORE 10092:RETURN
9603 RESTORE 10093:RETURN
10011 DATA 7,60,7,1,2,10,,
10012 DATA 4,75,4,1,2,10,,
10013 DATA 3,81,7,1,2,10,,
10014 DATA 4,75,8,1,2,10,,
10015 DATA 3,80,6,1,2,10,,
10016 DATA 1,80,6,1,3,3,,
10017 DATA 1,80,6,1,3,4,,
10018 DATA 5,80,7,1,2,10,,
10019 DATA 3,30,3,1,2,10,3,3
10020 DATA 4,70,7,1,2,10,,
10021 DATA 4,70,6,1,2,10,,
10022 DATA 2,70,6,1,3,4,,
10023 DATA 4,40,4,1,3,10,,
10024 DATA 3,70,5,1,2,10,,
10025 DATA 2,70,5,1,2,10,,
10026 DATA 4,60,5,1,2,10,,
10027 DATA 2,80,6,1,2,10,,
10028 DATA 4,50,6,1,3,10,,
10029 DATA 2,50,7,1,2,6,,
10030 DATA 3,84,7,1,2,10,,
10031 DATA 4,40,5,1,3,10,,
10032 DATA 5,40,6,1,3,10,,
10033 DATA 5,50,5,1,3,10,,
10034 DATA 5,50,5,1,3,8,,
10035 DATA 2,80,8,2,1,10,,
10036 DATA 8,70,7,2,1,10,2,1
10037 DATA 8,60,5,2,5,5,,
10038 DATA 5,60,5,2,5,10,,
10039 DATA 7,50,7,2,1,10,,
10040 DATA 4,50,7,2,1,10,,
10041 DATA 4,80,8,2,1,10,,
10042 DATA 6,70,4,1,5,3,,
10043 DATA 1,50,8,2,1,5,,
10044 DATA 7,60,4,1,2,5,,
10045 DATA 4,50,8,2,1,4,,
10046 DATA 3,50,8,2,1,3,,
10047 DATA 3,50,6,2,1,10,,
10048 DATA 5,76,5,3,2,10,,
10049 DATA 5,50,7,3,1,10,,
10050 DATA 3,84,7,3,2,10,,
10051 DATA 5,70,8,3,2,10,1,10
10052 DATA 3,50,8,3,1,3,,
10053 DATA 8,70,7,3,1,10,3,10
10055 DATA 5,75,7,3,1,10,,
10056 DATA 6,50,8,3,1,7,,
10057 DATA 3,70,7,3,1,10,,
10058 DATA 5,75,8,3,2,10,1,10
10059 DATA 6,70,8,3,1,10,2,10
10060 DATA 7,50,7,3,1,10,,
10061 DATA 5,50,5,3,3,8,,
10062 DATA 5,50,5,3,3,10,2,1
10063 DATA 4,40,5,3,3,10,,
10064 DATA 5,40,6,3,3,10,,
10065 DATA 7,70,7,3,2,10,3,10
10066 DATA 7,65,8,3,3,10,4,10
10067 DATA 5,40,6,3,3,10,,
10068 DATA 5,,,4,3,10,,
10069 DATA 6,50,4,4,5,8,,
10070 DATA 5,,,4,3,10,,
10071 DATA 7,50,6,4,5,10,1,1
10072 DATA 4,,,4,3,10,,
10073 DATA 5,60,5,4,2,6,,
10074 DATA 6,,,4,1,4,,
10075 DATA 4,50,7,4,4,6,,
10076 DATA 4,,,4,3,10,,
10077 DATA 5,,,4,3,10,4,2
10078 DATA 5,,,4,3,10,,
10079 DATA 5,40,6,4,3,5,,
10080 DATA ,,,5,2,10,1,10
10081 DATA ,,,5,3,10,2,3
10082 DATA ,,,5,2,10,3,2
10083 DATA ,,,5,3,10,1,3
10084 DATA ,,,5,1,,,
10085 DATA ,,,5,1,10,1,10
10086 DATA ,,,5,1,10,1,10
10087 DATA ,,,5,1,10,1,10
10088 DATA ,,,5,4,1,,
10089 DATA ,,,6,5,10,4,3
10090 DATA ,,,6,4,10,3,3
10091 DATA ,,,6,5,1,,
10092 DATA ,,,5,4,10,,
10093 DATA ,,,6,5,10,,
